plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 34

    namespace 'app.flashlight'

    defaultConfig {
        applicationId "app.flashlight"
        minSdk 23
        targetSdk 34
        versionCode 10
        versionName "0.9.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'GITHUB', '"https://github.com/kitfist0/ComposeFlashlight"'
        buildConfigField 'String', 'POLICY', '"https://github.com/kitfist0/ComposeFlashlight/blob/master/POLICY.md"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.7"
    }

    flavorDimensions = ["version"]

    productFlavors {
        demo {
            dimension "version"
            versionNameSuffix "-demo"
        }

        midnight {
            dimension "version"
            applicationId "midnight.flashlight"
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def flavorName = variant.flavorName
            def versionCode = variant.versionCode
            def versionName = variant.versionName
            def buildType = variant.buildType.name
            outputFileName = "${flavorName}-${versionCode}(${versionName})-${buildType}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Jetpack
    // https://developer.android.com/jetpack/androidx/releases/activity
    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    // https://developer.android.com/jetpack/androidx/releases/navigation
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'
    implementation 'androidx.navigation:navigation-compose:2.7.6'

    // Compose
    // https://developer.android.com/jetpack/androidx/releases/compose#versions
    implementation 'androidx.compose.ui:ui:1.5.4'
    implementation 'androidx.compose.ui:ui-tooling:1.5.4'
    implementation 'androidx.compose.material3:material3:1.1.2'

    // Compose-State-Events
    // https://github.com/leonard-palm/compose-state-events
    implementation 'com.github.leonard-palm:compose-state-events:2.2.0'

    // Hilt
    // https://github.com/google/dagger/releases
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.1.0'

    // DataStore
    // https://developer.android.com/jetpack/androidx/releases/datastore
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.5.4'
}
